'From Cuis7.1 [latest update: #6867] on 28 November 2024 at 11:07:48 am'!

!MethodSet methodsFor: 'private' stamp: 'jmv 11/28/2024 11:06:34'!
setFilteredList: newList
	"Establish newList as the new list if appropriate, and adjust the window title accordingly; if the new list is of the same size as the old, warn and do nothing"

	newList isEmpty
		ifTrue: [
			^ self inform: 'Nothing would be left in the list if you did that'].
	newList size = messageList size
		ifTrue: [
			^ self inform: 'That leaves the list unchanged'].
	self initializeMessageList: newList.
	self reformulateList.
	self changed: #relabel! !


!DirectoryEntry class methodsFor: 'base directories' stamp: 'jmv 11/28/2024 11:01:59'!
userBaseDirectory
	"The top directory for files generated by the system on behalf of the user
		userBaseDirectory/SavedChangeSets 						Folder for saving new ChangeSets
		userBaseDirectory/Packages: 								Folder for new code packages
		userBaseDirectory/UserChanges: 							Folder to store user changes files
		userBaseDirectory/FileOuts: 								Folder for filed out code
		userBaseDirectory/Logs: 									Folder for error logs and other logs
	DirectoryEntry userBaseDirectory
	See #cuisBaseDirectory. See #currentDirectory."

	UserBaseDirectory notNil ifTrue: [
		^ UserBaseDirectory ].

	Smalltalk getCurrentWorkingDirectory ifNotNil: [ :wd |
		UserBaseDirectory := self withAbsolutePathName: wd ].

	"If no OS current directory, use a sibling of #cuisBaseDirectory
	If OS current directory is same as #cuisBaseDirectory, take a sibling too"
	(UserBaseDirectory isNil or: [ UserBaseDirectory = self cuisBaseDirectory ]) ifTrue: [
		UserBaseDirectory := self cuisBaseDirectory parent / (self cuisBaseDirectory name, '-UserFiles').
		UserBaseDirectory assureExistence ].

	^ UserBaseDirectory! !

