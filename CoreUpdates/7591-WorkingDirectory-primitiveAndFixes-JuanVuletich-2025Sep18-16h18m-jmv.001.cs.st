'From Cuis7.5 [latest update: #7562] on 18 September 2025 at 4:51:12 pm'!

!FileIOAccessor methodsFor: 'primitives' stamp: 'eem 10/16/2024 10:14:17'!
primWorkingDirectory
	"Answer the utf8 encoded name of the VM process's working/current directory.
	Warning: Answer is an instance of String, but an invalid one (unless pure 7-bit ASCII).
	Please call
		DirectoryEntry workingDirectory
	instead.
	"

	<primitive: 'primitiveGetWorkingDirectory' module: 'FilePlugin' error: ec>
	self primitiveFailed.! !

!FileIOAccessor methodsFor: 'primitives' stamp: 'eem 10/16/2024 10:14:00'!
primWorkingDirectory: utf8Path
	"Set the VM process's working/current directory to be the argument, a utf8 string.
	Please call
		DirectoryEntry workingDirectory:
	instead.
	"

	<primitive: 'primitiveSetWorkingDirectory' module: 'FilePlugin' error: ec>
	self primitiveFailed.! !


!DirectoryEntry class methodsFor: 'system directories' stamp: 'eem 10/16/2024 10:22:10'!
workingDirectory
	"Answer the name of the VM process's working/current directory.
		DirectoryEntry workingDirectory
	"
	| string |
	string := FileIOAccessor default primWorkingDirectory.

	"Primitive answers an instance of String containing the UTF-8 bytes. If not pure ASCII,
	contents are invalid!! Convert it ASAP into a reasonable ByteArray + UnicodeString."
	(String isAscii: string) ifFalse: [
		ByteArray adoptInstance: string.
		string := UnicodeString fromUtf8Bytes: string ].

	^ self withAbsolutePathName: string! !

!DirectoryEntry class methodsFor: 'system directories' stamp: 'eem 10/16/2024 10:21:45'!
workingDirectory: pathName
	"Set the VM process's working working/current to be the argument."

	FileIOAccessor default primWorkingDirectory: pathName asUtf8Bytes.! !


!DirectoryEntry class methodsFor: 'base directories' stamp: 'jmv 9/18/2025 16:47:09'!
currentDirectory
	"The current directory.
	
	It is used, for unqualified names: `'stuff.txt' asFileEntry`, `'subfldr' asDirectoryEntry`. It is also used for relative paths if the full path is not specified: `'../sibling/data.txt'`.

	In Linux and macOS, it is the current directory in the OS shell that started us.

	Same in Windows if Cuis is started from a command line. But, for instance, if the image is dropped on the VM in a Windows explorer, and no OS current directory is available, it defaults to #userBaseDirectory.

	DirectoryEntry currentDirectory
	See #cuisBaseDirectory. See #userBaseDirectory.
	See #getCurrentWorkingDirectory"

	CurrentDirectory notNil ifTrue: [
		^ CurrentDirectory ].

	^ CurrentDirectory := self workingDirectory.! !

!DirectoryEntry class methodsFor: 'base directories' stamp: 'jmv 9/18/2025 16:49:57'!
userBaseDirectory
	"The top directory for files generated by the system on behalf of the user
		userBaseDirectory/SavedChangeSets 						Folder for saving new ChangeSets
		userBaseDirectory/Packages: 								Folder for new code packages
		userBaseDirectory/UserChanges: 							Folder to store user changes files
		userBaseDirectory/FileOuts: 								Folder for filed out code
		userBaseDirectory/Logs: 									Folder for error logs and other logs
	DirectoryEntry userBaseDirectory
	See #cuisBaseDirectory. See #currentDirectory."

	UserBaseDirectory notNil ifTrue: [
		^ UserBaseDirectory ].

	self workingDirectory ifNotNil: [ :wd |
		UserBaseDirectory := self withAbsolutePathName: wd ].

	"If no OS current directory, use a sibling of #cuisBaseDirectory
	If OS current directory is same as #cuisBaseDirectory, take a sibling too"
	(UserBaseDirectory isNil or: [ UserBaseDirectory = self cuisBaseDirectory ]) ifTrue: [
		UserBaseDirectory := self cuisBaseDirectory parent / (self cuisBaseDirectory name, '-UserFiles').
		UserBaseDirectory assureExistence ].

	^ UserBaseDirectory! !

!methodRemoval: SystemDictionary #primGetCurrentWorkingDirectoryWindows stamp: 'jmv 9/18/2025 16:50:17'!
SystemDictionary removeSelector: #primGetCurrentWorkingDirectoryWindows!
!methodRemoval: SystemDictionary #primGetCurrentWorkingDirectoryUnix stamp: 'jmv 9/18/2025 16:50:29'!
SystemDictionary removeSelector: #primGetCurrentWorkingDirectoryUnix!
!methodRemoval: SystemDictionary #getCurrentWorkingDirectory stamp: 'jmv 9/18/2025 16:50:02'!
SystemDictionary removeSelector: #getCurrentWorkingDirectory!
