'From Cuis6.3 [latest update: #6181] on 9 January 2024 at 10:20:48 am'!
!classDefinition: #Character category: #'Kernel-Text'!
Object immediateSubclass: #Character
	instanceVariableNames: ''
	classVariableNames: 'CompositionMapping DecompositionMapping UppercaseLettersTable LowercaseLettersTable CanonicalCombiningClasses NamedCharactersMap SymbolsLikeTable DiacriticalMarksTable '
	poolDictionaries: ''
	category: 'Kernel-Text'!

!Character class methodsFor: 'class initialization' stamp: 'jmv 4/11/2023 12:34:44'!
initialize
	"https://www.unicode.org/Public/UCD/latest/ucd/UnicodeData.txt
	http://www.unicode.org/L2/L1999/UnicodeData.html
	Character initialize
	"

	| fields codePoint codePointName generalCategory canonicalCombiningClass characterDecompositionMapping decomposition composition |
	UppercaseLettersTable := ByteArray newBits: 16r1FFFF. "Basic Multilingual Plane 	+ Supplementary Multilingual Plane"
	LowercaseLettersTable := ByteArray newBits: 16r1FFFF. "Basic Multilingual Plane 	+ Supplementary Multilingual Plane"
	SymbolsLikeTable := ByteArray newBits: 16r1FFFF. "Basic Multilingual Plane 	+ Supplementary Multilingual Plane"
	DiacriticalMarksTable := ByteArray newBits: 16r1FFFF. "Basic Multilingual Plane 	+ Supplementary Multilingual Plane"
	CanonicalCombiningClasses := Dictionary new.
	DecompositionMapping := Dictionary new.
	CompositionMapping := Dictionary new.
	(DirectoryEntry smalltalkImageDirectory // 'UnicodeData.txt') readStreamDo: [ :strm |
		[strm atEnd] whileFalse: [
			fields := strm nextLine substringsSeparatedBy: $;.
			(#(4 5 6) includes: fields first size) ifFalse: [ fields first print. self error: 'Bad UnicodeData.txt file' ].
			codePoint := ('16r', fields first) asNumber.
			codePointName := fields at: 2.
			generalCategory := fields at: 3.
			codePoint <= 16r1FFFF ifTrue: [
				({ 'Lu'. 'Lt' } includes: generalCategory) ifTrue: [ UppercaseLettersTable bitBooleanAt: codePoint+1 put: true ].
				({ 'Ll'. 'Lo' } includes: generalCategory) ifTrue: [ LowercaseLettersTable bitBooleanAt: codePoint+1 put: true ].
				({ 'Pc'. 'Po'. 'Sm'. 'Sc'. 'Sk'. 'So' } includes: generalCategory) ifTrue: [ SymbolsLikeTable bitBooleanAt: codePoint+1 put: true ].
				({ 'Mn'. 'Mc'. 'Me' } includes: generalCategory) ifTrue: [ DiacriticalMarksTable bitBooleanAt: codePoint+1 put: true ]].
			canonicalCombiningClass := (fields at: 4) asNumber.
			canonicalCombiningClass = 0 ifFalse: [
				CanonicalCombiningClasses at: codePoint put: canonicalCombiningClass ].
			characterDecompositionMapping := fields at: 6.
			(characterDecompositionMapping size > 0 and: [ characterDecompositionMapping first ~= $< ]) ifTrue: [
				decomposition := characterDecompositionMapping substrings collect: [ :s | ('16r', s) asNumber].
				DecompositionMapping at: codePoint put: decomposition.
				decomposition size > 1 ifTrue: [
					composition := CompositionMapping at: decomposition first ifAbsentPut: [Dictionary new].
					composition at: decomposition second put: codePoint.
					decomposition size > 2 ifTrue: [self error: decomposition printString print].
					]].
			"
			uppercaseMapping := fields at: 13. lowercaseMapping := fields at: 14. titlecaseMapping := fields at: 15.
			Random next > 10.999 | (generalCategory = 'So') ifTrue: [
				{codePoint hex. codePointName. generalCategory} print ].
			decimalDigitValue := fields at: 7. digitValue := fields at: 8. numericValue := fields at: 9.
			decimalDigitValue isEmpty & digitValue isEmpty & numericValue isEmpty ifFalse: [
				{'numeric'. codePoint. codePointName. generalCategory. decimalDigitValue. digitValue. numericValue} print].
			"
			]].
	self initializeNamedCharactersMap! !

!Character class methodsFor: 'class initialization' stamp: 'len 5/19/2023 17:27:22'!
initializeNamedCharactersMap
	"Dictionary of named characters that we can enter in an editor using \name.
	Please keep the names short and try to follow the naming convention used in LaTeX."

	NamedCharactersMap := Dictionary new.
	"Superscripts:"
	4 to: 9 do: [:each| NamedCharactersMap at: '^', each printString put: (self codePoint: 16r2070 + each)].
	"Subscripts:"
	0 to: 9 do: [:each| NamedCharactersMap at: '_', each printString put: (self codePoint: 16r2080 + each)].
	'hklmnpst' withIndexDo: [:each :i| NamedCharactersMap at: '_', each asString put: (self codePoint: 16r2094 + i)].
	'iruv' withIndexDo: [:each :i| NamedCharactersMap at: '_', each asString put: (self codePoint: 16r1D61 + i)].
	"Combining characters:"
	#(
		(acute 16r0301) ('''' 16r0301) (bar 16r0304) (breve 16r0306) (check 16r030C)
		(grave 16r0300) ('`' 16r0300) (hat 16r0302) (not 16r0338) (overline 16r0305)
		(tilde 16r0303) ('~' 16r0303) ('"' 16r0308) (vec 16r20D7)
	) do: [:each| NamedCharactersMap at: each first put: (self codePoint: each second)].
	#(
		"Some more superscripts and subscripts:"
		('^0' $â°) ('^1' $Â¹) ('^2' $Â²) ('^3' $Â³) ('^i' $â±) ('^n' $â¿) ('_j' $â±¼) ('^+' $âº) ('^-' $â») ('_+' $â‚Š) ('_-' $â‚‹)
		"Arrows:"
		(left $â†) (right $â†’) (up $â†‘) (down $â†“) (leftright $â†”) (updown $â†•)
		(Left $â‡) (Up $â‡‘) (Right $â‡’) (Down $â‡“) (Leftright $â‡”) (Updown $â‡•)
		(gets $â†) (to $â†’) (mapsto $â†¦) (mapsfrom $â†¤) (implies $âŸ¹) (impliedby $âŸ¸) (iff $âŸº)
		(from $â†) (iso $â†”) (From $â‡) (To $â‡’) (dfrom $â‡‡) (dto $â‡‰) "non-standard"
		"Greek alphabet:"
		(alpha $Î±) (beta $Î²) (gamma $Î³) (delta $Î´) (epsilon $Îµ) (zeta $Î¶) (eta $Î·) (theta $Î¸) (iota $Î¹) (kappa $Îº) (lambda $Î») (mu $Î¼) (nu $Î½) (xi $Î¾) (omicron $Î¿) (pi $Ï€) (rho $Ï) (sigmaf $Ï‚) (sigma $Ïƒ) (tau $Ï„) (upsilon $Ï…) (phi $Ï†) (chi $Ï‡) (psi $Ïˆ) (omega $Ï‰) (thetasym $Ï‘) (upsih $Ï’) (piv $Ï–)
		(Alpha $Î‘) (Beta $Î’) (Gamma $Î“) (Delta $Î”) (Epsilon $Î•) (Zeta $Î–) (Eta $Î—) (Theta $Î˜) (Iota $Î™) (Kappa $Îš) (Lambda $Î›) (Mu $Îœ) (Nu $Î) (Xi $Î) (Omicron $ÎŸ) (Pi $Î ) (Rho $Î¡) (Sigma $Î£) (Tau $Î¤) (Upsilon $Î¥) (Phi $Î¦) (Chi $Î§) (Psi $Î¨) (Omega $Î©)
		"Blackboard bold:"
		(A $ğ”¸) (B $ğ”¹) (C $â„‚) (D $ğ”») (E $ğ”¼) (F $ğ”½) (G $ğ”¾) (H $â„) (I $ğ•€) (J $ğ•) (K $ğ•‚) (L $ğ•ƒ) (M $ğ•„) (N $â„•) (O $ğ•†) (P $â„™) (Q $â„š) (R $â„) (S $ğ•Š) (T $ğ•‹) (U $ğ•Œ) (V $ğ•) (W $ğ•) (X $ğ•) (Y $ğ•) (Z $â„¤)
		"Math symbols:"
		(oplus $âŠ•) (otimes $âŠ—) (ominus $âŠ–) (odot $âŠ™) (oslash $âŠ˜)
		(times $Ã—) (div $Ã·) (circ $âˆ˜) (bullet $âˆ™) (cdot $Â·) (pm $Â±) (wr $â‰€)
		(perp $âŠ¥)		
		(cap $âˆ©) (cup $âˆª) (sqcap $âŠ“) (sqcup $âŠ”) (wedge $âˆ§) (vee $âˆ¨) (perp $âŠ¥) (parallel $âˆ¥)	
		(in $âˆˆ) (notin $âˆ‰) (exists $âˆƒ) (nexists $âˆ„) (forall $âˆ€) (emptyset $âˆ…) (infty $âˆ) (aleph $â„µ) (ell $â„“)
		(Im $â„‘) (Re $â„œ) (wp $â„˜)
		(sqrt $âˆš) (partial $âˆ‚) (deg $Â°)
(leq $â‰¤) (geq $â‰¥) (neq $â‰ ) (prec $â‰º) (succ $â‰») (simeq $â‰ƒ) (cong $â‰…) (equiv $â‰¡) (approx $â‰ˆ) (subset $âŠ‚) (supset $âŠƒ)
		(ldots $â€¦) (cdots $â‹¯) (ddots $â‹±) (iddots $â‹°) (vdots $â‹®) "(prime $â€²) (dprime $â€³) (tprime $â€´)"
		"Other:"
		(langle $âŸ¨) (rangle $âŸ©) (lceil $âŒˆ) (rceil $âŒ‰) (lfloor $âŒŠ) (rfloor $âŒ‹) (ltimes $â‹‰) (rtimes $â‹Š) 
		(lowast $â) (dag $â€ ) (ddag $â€¡) (emdash $â€”) (endash $â€“) (questiondown $Â¿) (exclamdown $Â¡)
		(euro $â‚¬) (pounds $Â£) (celsius $â„ƒ) (farenheit $â„‰)
		(registered $Â®) (copyright $Â©) (trademark $â„¢) (snowman $â˜ƒ) (section $Â§))
	do: [:each| NamedCharactersMap at: each first put: each second].
	
	"Some synonyms:"
	#((prod sqcap) (coprod sqcup) (stack sqcap) (augment sqcup) (and wedge) (or vee) (xor oplus))
	do: [:each| NamedCharactersMap at: each first put: (NamedCharactersMap at: each second)]! !

!Character class methodsFor: 'accessing untypeable characters' stamp: 'jmv 4/3/2023 14:40:54'!
zeroWidthSpace
	^self  codePoint: 16r200B! !

!Character class methodsFor: 'services' stamp: 'jmv 12/11/2022 19:05:09'!
canonicalCompositionOf: numericCodePoint and: combiningCodePoint

	| dict |
	dict := CompositionMapping at: numericCodePoint
		ifAbsent: [^nil ].
	^dict at: combiningCodePoint ifAbsent: nil! !

!Character class methodsFor: 'services' stamp: 'jmv 12/11/2022 19:06:32'!
canonicalDecomposition: numericCodePoint on: aStream

	| decomposition |
	decomposition := DecompositionMapping at: numericCodePoint
		ifAbsent: [^aStream nextPutCodePoint: numericCodePoint].
	decomposition do: [ :each |
		self canonicalDecomposition: each on: aStream ]! !

!Character class methodsFor: 'services' stamp: 'jmv 12/11/2022 19:14:16'!
combiningClass: numericCodePoint
	(self isDiacriticalMark: numericCodePoint) ifFalse: [ ^0 ].
	^CanonicalCombiningClasses at: numericCodePoint ifAbsent: [ 	0 ]! !

!Character class methodsFor: 'services' stamp: 'jmv 12/2/2022 14:15:14'!
isCombiningStarter: numericCodePoint
	^ (self combiningClass: numericCodePoint) = 0! !

!Character class methodsFor: 'services' stamp: 'jmv 12/2/2022 14:13:46'!
isDiacriticalMark: numericCodePoint
	"
	(Character isDiacriticalMark: 16r0041) 
	(Character codePoint: 16r0041) isDiacriticalMark
	(Character isDiacriticalMark: 16r20D7)
	(Character codePoint: 16r20D7) isDiacriticalMark
	"
	| i |
	i := numericCodePoint + 1.
	DiacriticalMarksTable bitSize < i ifTrue: [ ^false ].
	^ DiacriticalMarksTable bitBooleanAt: i.! !

!Character class methodsFor: 'constants' stamp: 'len 12/1/2022 12:23:44'!
namedCharactersMap
	"Dictionary of named characters that we can enter in an editor using \name."

	^ NamedCharactersMap! !

!classDefinition: #Character category: #'Kernel-Text'!
Object immediateSubclass: #Character
	instanceVariableNames: ''
	classVariableNames: 'CanonicalCombiningClasses CompositionMapping DecompositionMapping DiacriticalMarksTable LowercaseLettersTable NamedCharactersMap SymbolsLikeTable UppercaseLettersTable'
	poolDictionaries: ''
	category: 'Kernel-Text'!
Character initialize!
